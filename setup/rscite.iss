; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

;Autore :   Roberto Rossi
;Versione : 4.0.0
;Web      : http://www.redchar.net

;Compatible with Inno Setup 6 or later

;how check redist vc
;https://pingfu.net/how-to-detect-which-version-of-visual-c-runtime-is-installed
;https://stackoverflow.com/questions/29937568/how-can-i-find-the-product-guid-of-an-installed-msi-setup
;https://github.com/vinaypamnani/wmie2/issues
;{6F0267F3-7467-350D-A8C8-33B72E3658D8} Microsoft Visual C++ 2017 x86 Additional Runtime - 14.14.26429
;{7753EC39-3039-3629-98BE-447C5D869C09} Microsoft Visual C++ 2017 x86 Minimum Runtime - 14.14.26429
;{03EBF679-E886-38AD-8E70-28658449F7F9} Microsoft Visual C++ 2017 x64 Minimum Runtime - 14.14.26429
;{B12F584A-DE7A-3EE3-8EC4-8A64DBC0F2A7} Microsoft Visual C++ 2017 x64 Additional Runtime - 14.14.26429

#define RSciTEVersion 86

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{A4729A09-46FB-4766-9D9D-24358E58E453}

AppName=RSciTE
AppVerName=RSciTE {#RSciTEVersion}
OutputDir=d:\temp\output\
OutputBaseFilename=setup-rscite-{#RSciTEVersion}

UninstallDisplayName=RSciTE
AppPublisher=Roberto Rossi
AppPublisherURL=http://www.redchar.net/
AppSupportURL=http://www.redchar.net/
AppUpdatesURL=http://www.redchar.net/
DefaultDirName={commonpf}\RSciTE
DefaultGroupName=RSciTE
LicenseFile=..\sources\distro\Licenze_distribuzione.txt
Compression=lzma
SolidCompression=no
;WizardImageFile=rscite.bmp
WizardStyle=modern
WizardImageFile=compiler:wizmodernimage-is.bmp
WizardSmallImageFile=compiler:wizmodernsmallimage-is.bmp
SetupIconFile=rsetup.ico
UninstallRestartComputer=true
DisableProgramGroupPage=yes
Uninstallable=not WizardIsTaskSelected('portablemode')
UninstallDisplayIcon=rsetup.ico
DisableWelcomePage=false
AlwaysShowDirOnReadyPage=yes
DisableDirPage=no
ArchitecturesAllowed=x64 ia64
ArchitecturesInstallIn64BitMode=x64 ia64

[Languages]
Name: english; MessagesFile: compiler:Default.isl
Name: italian; MessagesFile: compiler:Languages\Italian.isl

[Tasks]
; TODO : da tradurre la scritta per la modalità portabile
Name: portablemode; Description: {cm:PortableVersion}; GroupDescription: {cm:PortableVersionGroup}; Flags: unchecked;
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked;

[Files]
Source: ..\sources\distro\*; DestDir: {app}; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
;Source: ..\sources\distro_dll\wscitecm_it.dll; DestDir: {app}; Flags: onlyifdoesntexist uninsrestartdelete; Check: not IsWin64; Tasks: ; Languages: italian
;Source: ..\sources\distro_dll\wscitecm_en.dll; DestDir: {app}; Flags: onlyifdoesntexist uninsrestartdelete; Check: not IsWin64; Tasks: ; Languages: english
Source: ..\sources\distro_dll\wscitecm64_it.dll; DestDir: {app}; Flags: onlyifdoesntexist uninsrestartdelete; Check: IsWin64; Languages: italian
Source: ..\sources\distro_dll\wscitecm64_en.dll; DestDir: {app}; Flags: onlyifdoesntexist uninsrestartdelete; Check: IsWin64; Tasks: ; Languages: english
;Elimina superflui per trasformare versione italiana in inglese
Source: ..\sources\distro\loctools.properties; DestDir: {app}; Flags: deleteafterinstall; Tasks: ; Languages: english
Source: ..\sources\distro\locale.properties; DestDir: {app}; Flags: deleteafterinstall; Tasks: ; Languages: english
Source: ..\sources\distro\luascr\locale.properties; DestDir: {app}\luascr\; Flags: deleteafterinstall; Tasks: ; Languages: english

[Icons]
;SciTE
Name: {group}\{cm:IconRscite}; Filename: "{app}\SciTE.exe"; IconFilename: {app}\scite.ico; Tasks: not portablemode;
;dnGrep
Name: {group}\{cm:IcondnGREP}; Filename: "{app}\tools\dngrep\dnGREP.exe"; Tasks: not portablemode;
;Frhed
Name: {group}\{cm:IconFrhed}; Filename: "{app}\tools\winmerge\frhed\Frhed.exe"; Tasks: not portablemode;
;KDiff
;Name: {group}\{cm:IconKDiff}; Filename: "{app}\tools\kdiff\kdiff3.exe"; Tasks: not portablemode;
;WinMerge
Name: {group}\{cm:IconWinMerge}; Filename: "{app}\tools\winmerge\WinMergeU.exe"; Tasks: not portablemode;
;Home Page
Name: {group}\Roberto Rossi Home Page; Filename: "{win}\explorer.exe"; Parameters: "http://www.redchar.net/"; IconFilename: "{win}\explorer.exe"; IconIndex: 14; Tasks: not portablemode;

Name: {commondesktop}\RSciTE; Filename: {app}\SciTE.exe; Tasks: desktopicon;
;Name: {usersendto}\SciTE; Filename: {app}\SciTE.exe; IconFilename: {app}\scite.ico; Tasks: not portablemode;

[Run]

[Registry]

[UninstallDelete]
;Name: {userappdata}\RSciTE; Type: filesandordirs; Tasks: ; Languages: 

[CustomMessages]
english.PrevInstall=Is present another version of RSciTE! I recommend to uninstalling the previous version before proceeding.%n%nDo you want open Control Panel to uninstall previous version of RSciTE?
italian.PrevInstall=E' presente una precedente versione di RSciTE.%nPer evitare qualsiasi tipo di conflitto suggerisco di procedere, prima di proseguire, ad una DISINSTALLAZIONE dell'attuale RSciTE tramite il pannello di controllo.%n%nE' comunque possibile continuare e tentare di aggiornare la versione corrente. %n%nSi desidera aprire il pannello di controllo per disinstallare la precedente versione di RSciTE?
english.PortableVersion=Don't install RSciTE, but extract the files for the portable version.
italian.PortableVersion=Non installare il programma, ma estrai solo i file della versione Portabile.
english.PortableVersionGroup=RSciTE Portable Version
italian.PortableVersionGroup=Versione Portabile di RSciTE
english.IconRscite=SciTE - Text Editor
italian.IconRscite=SciTE - Editor di testo
english.IcondnGREP=dnGREP - Find and Replace
italian.IcondnGREP=dnGREP - Trova e Sostituisci
english.IconFrhed=Frhed - Hex Editor
italian.IconFrhed=Frhed - Editor Esadecimale
english.IconKDiff=KDiff - Compare Files
italian.IconKDiff=KDiff - Confronto File
english.IconWinMerge=WinMerge - Compare Files
italian.IconWinMerge=WinMerge - Confronto File
english.IconRegexerator=Regexerator - RegEx Tester
italian.IconRegexerator=Regexerator - Tester Espressioni Regolari
english.IconRSciTEDOC=RSciTE - Guida alle Caratteristiche
italian.IconRSciTEDOC=RSciTE - Functions Guide

[Code]
var
  FirstInstallation: Boolean;

#IFDEF UNICODE
    #DEFINE AW "W"
#ELSE
    #DEFINE AW "A"
#ENDIF
type
    INSTALLSTATE = Longint;
const
    INSTALLSTATE_INVALIDARG = -2;  // An invalid parameter was passed to the function.
    INSTALLSTATE_UNKNOWN = -1;     // The product is neither advertised or installed.
    INSTALLSTATE_ADVERTISED = 1;   // The product is advertised but not installed.
    INSTALLSTATE_ABSENT = 2;       // The product is installed for a different user.
    INSTALLSTATE_DEFAULT = 5;      // The product is installed for the current user.

    //DOWNLOADS FOR VISUAL C++ 2017
    VC_REDIST2017_URL = 'https://aka.ms/vs/15/release/VC_redist.x86.exe';
    VC_REDIST2017_URL_x64 = 'https://aka.ms/vs/15/release/VC_redist.x64.exe';

    //OPTIONS
    VC_2017_REDIST =     '{7753EC39-3039-3629-98BE-447C5D869C09}'; //Microsoft.VS.VC_RuntimeMinimumVSU_x86
    VC_2017_REDIST_x64 = '{03EBF679-E886-38AD-8E70-28658449F7F9}'; //Microsoft.VS.VC_RuntimeMinimumVSU_amd64

    function MsiQueryProductState(szProduct: String): INSTALLSTATE;
    external 'MsiQueryProductState{#AW}@msi.dll stdcall';

function VCVersionInstalled(const ProductID: String): Boolean;
begin
    Result := MsiQueryProductState(ProductID) = INSTALLSTATE_DEFAULT;
end;

procedure CurStepChanged(CurStep: TSetupStep);
var 
    ResultCode: Integer;
begin
  if (CurStep=ssInstall) then //controllo installazione precedente
  begin 
    FirstInstallation := True; //prima installazione
    
    if FileExists(ExpandConstant('{app}\scite.exe')) then
    begin
      if MsgBox(ExpandConstant('{cm:PrevInstall}'), mbConfirmation, MB_YESNO or MB_DEFBUTTON2) = IDYES then
      begin      
        //rundll32.exe shell32.dll,Control_RunDLL Appwiz.cpl
        Exec(ExpandConstant('{sys}\rundll32.exe'), 'shell32.dll,Control_RunDLL Appwiz.cpl', '', SW_SHOW, ewNoWait, ResultCode);
        // user clicked No
        Abort();
      end else
      begin
        // Delete files matching *.properties from old rscite versions
        DelTree(ExpandConstant('{app}\*.properties'), False, True, False);
        // Delete icons from old rscite versions
        DeleteFile(ExpandConstant('{app}\KDiff.lnk'));
        DeleteFile(ExpandConstant('{app}\WinMerge.lnk'));
        DeleteFile(ExpandConstant('{app}\Frhed.lnk'));
        FirstInstallation := False; //upgrade in corso
      end;
    end;
  end; //endpreinstallazione

  if (CurStep=ssPostInstall) then //installazione conclusive
  begin

    if (not WizardIsTaskSelected('portablemode')) then
    begin
        //gestione runtimes
        if NOT VCVersionInstalled(VC_2017_REDIST_x64) then
        begin
            //installazione runtime 64bit
            Exec(ExpandConstant('{app}\tools\runtimes\vcredist_x64-2017.exe'), '/install /passive /norestart', '', SW_SHOW, ewWaitUntilTerminated, ResultCode)
        end;

        //registrazione menu contestuale a 64 bit
        if FileExists(ExpandConstant('{app}\wscitecm64_en.dll')) then
        begin
            RegisterServer(Is64BitInstallMode, ExpandConstant('{app}\wscitecm64_en.dll'), True);
        end;
        if FileExists(ExpandConstant('{app}\wscitecm64_it.dll')) then
        begin
            RegisterServer(Is64BitInstallMode, ExpandConstant('{app}\wscitecm64_it.dll'), True);
        end;
        
        //rimozione eventuali configurazioni di dnGrep
        DeleteFile(ExpandConstant('{userappdata}\dnGREP\dnGREP.Settings.dat'));
    end; //not portablemode

  end; //end conclusione


end; //endCurStepChanged


