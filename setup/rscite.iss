; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

;Autore :   Roberto Rossi
;Versione : 3.0.0
;Web      : http://www.redchar.net

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{A4729A09-46FB-4766-9D9D-24358E58E453}
AppName=RSciTE
#include "appvername.iss"
;AppVerName=RSciTE
UninstallDisplayName=RSciTE
;OutputBaseFilename=rscite_setup3_1_0-29
AppPublisher=Roberto Rossi
AppPublisherURL=http://www.redchar.net/
AppSupportURL=http://www.redchar.net/
AppUpdatesURL=http://www.redchar.net/
DefaultDirName={pf}\RSciTE
DefaultGroupName=RSciTE
LicenseFile=..\sources\distro\Licenze_distribuzione.txt
Compression=lzma
SolidCompression=no
;WizardImageFile=rscite.bmp
SetupIconFile=rsetup.ico
UninstallRestartComputer=true
DisableProgramGroupPage=yes
Uninstallable=not IsTaskSelected('portablemode')

[Languages]
Name: english; MessagesFile: compiler:Default.isl
Name: italian; MessagesFile: compiler:Languages\Italian.isl

[Tasks]
; TODO : da tradurre la scritta per la modalità portabile
Name: portablemode; Description: {cm:PortableVersion}; GroupDescription: {cm:PortableVersionGroup}; Flags: unchecked;
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked;

[Files]
Source: ..\sources\distro\*; DestDir: {app}; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: ..\sources\distro_dll\wscitecm_it.dll; DestDir: {app}; Flags: restartreplace uninsrestartdelete; Check: not IsWin64; Tasks: ; Languages: italian
Source: ..\sources\distro_dll\wscitecm_en.dll; DestDir: {app}; Flags: restartreplace uninsrestartdelete; Check: not IsWin64; Tasks: ; Languages: english
Source: ..\sources\distro_dll\wscitecm64_it.dll; DestDir: {app}; Flags: restartreplace uninsrestartdelete; Check: IsWin64; Languages: italian
Source: ..\sources\distro_dll\wscitecm64_en.dll; DestDir: {app}; Flags: restartreplace uninsrestartdelete; Check: IsWin64; Tasks: ; Languages: english
;Elimina superflui per trasformare versione italiana in inglese
Source: ..\sources\distro\loctools.properties; DestDir: {app}; Flags: deleteafterinstall; Tasks: ; Languages: english
Source: ..\sources\distro\locale.properties; DestDir: {app}; Flags: deleteafterinstall; Tasks: ; Languages: english
Source: ..\sources\distro\luascr\locale.properties; DestDir: {app}\luascr\; Flags: deleteafterinstall; Tasks: ; Languages: english

[Icons]
Name: {group}\SciTE; Filename: {app}\SciTE.exe; IconFilename: {app}\scite.ico; Tasks: not portablemode;
Name: {group}\Doc\RSciTE Doc; Filename: {app}\hlpscite\rscite\guida_a_rscite.pdf; Tasks: not portablemode; Languages: italian
Name: {group}\Doc\SciTE Doc; Filename: {app}\hlpscite\scite.html; Tasks: not portablemode; Languages: 
Name: {group}\Doc\Lua Doc; Filename: {app}\hlplua\lua.chm; Tasks: not portablemode; Languages: 
Name: {group}\Doc\Tidy Doc; Filename: {app}\tools\tidy\htmldoc\Overview.html; Tasks: not portablemode; Languages: 
Name: {group}\Doc\KDiff Doc; Filename: {app}\tools\kdiff\doc\index.html; Tasks: not portablemode; Languages: 
Name: {group}\Doc\WinMerge Doc; Filename: {app}\tools\winmerge\Docs\WinMerge.chm; Tasks: not portablemode; Languages: 
Name: {group}\Roberto Rossi Home Page; Filename: http://www.redchar.net/; Tasks: not portablemode; Languages: 
Name: {commondesktop}\RSciTE; Filename: {app}\SciTE.exe; Tasks: desktopicon
Name: {sendto}\SciTE; Filename: {app}\SciTE.exe; IconFilename: {app}\scite.ico; Tasks: not portablemode; Languages: 

[Run]

[Registry]

[UninstallDelete]
Name: {app}\wscitecm_en.dll; Type: files; Tasks: ; Languages: 
Name: {app}\wscitecm_it.dll; Type: files
Name: {userappdata}\RSciTE; Type: filesandordirs; Tasks: ; Languages: 

[CustomMessages]
english.PrevInstall=Is presente another version of RSciTE! Do you want proceed with upgrade?
italian.PrevInstall=E' presente una precedente Installazione di RSciTE. Si desidera procedere all'aggiornamento?
english.PortableVersion=Don't installa RSciTE, but extract the files for the portable version.
italian.PortableVersion=Non installare il programma, ma estrai solo i file della versione Portabile.
english.PortableVersionGroup=RSciTE Portable Version
italian.PortableVersionGroup=Versione Portabile di RSciTE


[Code]
var
  FirstInstallation: Boolean;

#IFDEF UNICODE
    #DEFINE AW "W"
#ELSE
    #DEFINE AW "A"
#ENDIF
type
    INSTALLSTATE = Longint;
const
    INSTALLSTATE_INVALIDARG = -2;  // An invalid parameter was passed to the function.
    INSTALLSTATE_UNKNOWN = -1;     // The product is neither advertised or installed.
    INSTALLSTATE_ADVERTISED = 1;   // The product is advertised but not installed.
    INSTALLSTATE_ABSENT = 2;       // The product is installed for a different user.
    INSTALLSTATE_DEFAULT = 5;      // The product is installed for the current user.

    //DOWNLOADS FOR VISUAL C++ 2013
    VC_REDIST2013_URL = 'http://download.microsoft.com/download/2/E/6/2E61CFA4-993B-4DD4-91DA-3737CD5CD6E3/vcredist_x86.exe';
    VC_REDIST2013_URL_x64 = 'http://download.microsoft.com/download/2/E/6/2E61CFA4-993B-4DD4-91DA-3737CD5CD6E3/vcredist_x64.exe';

    //OPTIONS
    VC_2013_REDIST = '{13A4EE12-23EA-3371-91EE-EFB36DDFFF3E}'; //Microsoft.VS.VC_RuntimeMinimumVSU_x86,v12
    VC_2013_REDIST_x64 = '{A749D8E6-B613-3BE3-8F5F-045C84EBA29B}'; //Microsoft.VS.VC_RuntimeMinimumVSU_amd64,v12

    function MsiQueryProductState(szProduct: String): INSTALLSTATE;
    external 'MsiQueryProductState{#AW}@msi.dll stdcall';

function VCVersionInstalled(const ProductID: String): Boolean;
begin
    Result := MsiQueryProductState(ProductID) = INSTALLSTATE_DEFAULT;
end;

procedure CurStepChanged(CurStep: TSetupStep);
var 
    ResultCode: Integer;
begin
  if (CurStep=ssInstall) then //controllo installazione precedente
  begin 
    FirstInstallation := True; //prima installazione
    
    if FileExists(ExpandConstant('{app}\scite.exe')) then
    begin
      if MsgBox(ExpandConstant('{cm:PrevInstall}'), mbConfirmation, MB_YESNO) = IDNO then
      begin
        // user clicked No
        Abort();
      end else
      begin
        // Delete files matching *.properties from old rscite versions
        DelTree(ExpandConstant('{app}\*.properties'), False, True, False);
        // Delete icons from old rscite versions
        DeleteFile(ExpandConstant('{app}\KDiff.lnk'));
        DeleteFile(ExpandConstant('{app}\WinMerge.lnk'));
        DeleteFile(ExpandConstant('{app}\Frhed.lnk'));
        FirstInstallation := False; //upgrade in corso
      end;
    end;
  end; //endpreinstallazione

  if (CurStep=ssPostInstall) then //installazione conclusive
  begin

    if (not IsTaskSelected('portablemode')) then
    begin
        //gestione runtimes
        if (IsWin64)then
        begin
            //win 64
            if NOT VCVersionInstalled(VC_2013_REDIST) then
            begin
                //installazione runtime 32bit
                Exec(ExpandConstant('{app}\tools\runtimes\vcredist_x86-2013.exe'), '/install /passive /norestart', '', SW_SHOW, ewWaitUntilTerminated, ResultCode)
            end;
            if NOT VCVersionInstalled(VC_2013_REDIST_x64) then
            begin
                //installazione runtime 64bit
                Exec(ExpandConstant('{app}\tools\runtimes\vcredist_x64-2013.exe'), '/install /passive /norestart', '', SW_SHOW, ewWaitUntilTerminated, ResultCode)
            end;
        end else
        begin
            //win 32
            if NOT VCVersionInstalled(VC_2013_REDIST) then
            begin
                //installazione runtime 32bit
                Exec(ExpandConstant('{app}\tools\runtimes\vcredist_x86-2013.exe'), '/install /passive /norestart', '', SW_SHOW, ewWaitUntilTerminated, ResultCode)
            end;
        end;

        //registrazione menu contestuale a 32 bit
        if FileExists(ExpandConstant('{app}\wscitecm_en.dll')) then
        begin
          RegisterServer(false, ExpandConstant('{app}\wscitecm_en.dll'), True);
        end;
        if FileExists(ExpandConstant('{app}\wscitecm_it.dll')) then
        begin
          RegisterServer(false, ExpandConstant('{app}\wscitecm_it.dll'), True);
        end;

        if (IsWin64)then
        begin
          //registrazione menu contestuale a 64 bit
          if FileExists(ExpandConstant('{app}\wscitecm64_en.dll')) then
          begin
            RegisterServer(true, ExpandConstant('{app}\wscitecm64_en.dll'), True);
          end;
          if FileExists(ExpandConstant('{app}\wscitecm64_it.dll')) then
          begin
            RegisterServer(true, ExpandConstant('{app}\wscitecm64_it.dll'), True);
          end;
        end;
    end; //not portablemode

  end; //end conclusione


end; //endCurStepChanged


