; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{A4729A09-46FB-4766-9D9D-24358E58E453}
AppName=RSciTE
#include "appvername.iss"
;AppVerName=RSciTE
UninstallDisplayName=RSciTE
;OutputBaseFilename=rscite_setup3_1_0-29
AppPublisher=Roberto Rossi
AppPublisherURL=http://www.redchar.net/
AppSupportURL=http://www.redchar.net/
AppUpdatesURL=http://www.redchar.net/
DefaultDirName={pf}\RSciTE
DefaultGroupName=RSciTE
LicenseFile=..\sources\distro\Licenze_distribuzione.txt
Compression=lzma
SolidCompression=no
;WizardImageFile=rscite.bmp
SetupIconFile=rsetup.ico
UninstallRestartComputer=true
DisableProgramGroupPage=yes

[Languages]
Name: english; MessagesFile: compiler:Default.isl
Name: italian; MessagesFile: compiler:Languages\Italian.isl

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}

[Files]
Source: ..\sources\distro\*; DestDir: {app}; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: ..\sources\distro_dll\wscitecm_it.dll; DestDir: {app}; Flags: onlyifdoesntexist uninsrestartdelete; Tasks: ; Languages: italian
Source: ..\sources\distro_dll\wscitecm64_it.dll; DestDir: {app}; Flags: onlyifdoesntexist uninsrestartdelete; Languages: italian
Source: ..\sources\distro_dll\wscitecm_en.dll; DestDir: {app}; Flags: onlyifdoesntexist uninsrestartdelete; Tasks: ; Languages: english
Source: ..\sources\distro_dll\wscitecm64_en.dll; DestDir: {app}; Flags: onlyifdoesntexist uninsrestartdelete; Tasks: ; Languages: english
Source: ..\sources\distro\loctools.properties; DestDir: {app}; Flags: deleteafterinstall; Tasks: ; Languages: english
Source: ..\sources\distro\locale.properties; DestDir: {app}; Flags: deleteafterinstall; Tasks: ; Languages: english
Source: ..\sources\distro\luascr\locale.properties; DestDir: {app}\luascr\; Flags: deleteafterinstall; Tasks: ; Languages: english

[Icons]
Name: {group}\SciTE; Filename: {app}\SciTE.exe; IconFilename: {app}\scite.ico
;Name: {group}\KDiff; Filename: {app}\tools\kdiff\kdiff3.exe; Tasks: ; Languages:
;Name: {group}\WinMerge; Filename: {app}\tools\winmerge\WinMergeU.exe; Tasks: ; Languages:
;Name: {group}\Frhed; Filename: {app}\tools\frhed\Frhed.exe; Tasks: ; Languages:
;Name: {group}\Utilità\Inserisci SciTE in Esplora Risorse; Filename: {app}\regContextMenu.exe; Tasks: ; Languages: italian
;Name: {group}\Utilities\Insert SciTE into Explorer; Filename: {app}\regContextMenu.exe; Tasks: ; Languages: english

Name: {group}\Doc\RSciTE Doc; Filename: {app}\hlpscite\rscite\guida_a_rscite.pdf; Tasks: ; Languages: italian
Name: {group}\Doc\SciTE Doc; Filename: {app}\hlpscite\scite.html; Tasks: ; Languages: 
Name: {group}\Doc\Lua Doc; Filename: {app}\hlplua\lua.chm; Tasks: ; Languages: 
Name: {group}\Doc\Tidy Doc; Filename: {app}\tools\tidy\htmldoc\Overview.html; Tasks: ; Languages: 
Name: {group}\Doc\KDiff Doc; Filename: {app}\tools\kdiff\doc\index.html; Tasks: ; Languages: 
Name: {group}\Doc\WinMerge Doc; Filename: {app}\tools\winmerge\Docs\WinMerge.chm; Tasks: ; Languages: 
Name: {group}\Roberto Rossi Home Page; Filename: http://www.redchar.net/; Tasks: ; Languages: 
Name: {commondesktop}\RSciTE; Filename: {app}\SciTE.exe; Tasks: desktopicon
Name: {sendto}\SciTE; Filename: {app}\SciTE.exe; IconFilename: {app}\scite.ico

[Run]

[Registry]

[UninstallDelete]
Name: {app}\wscitecm_en.dll; Type: files; Tasks: ; Languages: 
Name: {app}\wscitecm_it.dll; Type: files
Name: {userappdata}\RSciTE; Type: filesandordirs; Tasks: ; Languages: 

[CustomMessages]
english.PrevInstall=Is presente a previous version of RSciTE! Do you want proceed with upgrade?
italian.PrevInstall=E' presente una precedente Installazione di RSciTE. Si desidera procedere all'aggiornamento?

[Code]
var
  FirstInstallation: Boolean;

procedure CurStepChanged(CurStep: TSetupStep);
var
  ResultCode: Integer;
begin

  if (CurStep=ssPostInstall) then
  begin //post installazione

    if IsWin64 then
    begin
      //avvio installazione runtime VC 64bit
      //if FileExists(ExpandConstant('{app}\tools\runtimes\vc8\vcredist_x64.exe')) then
      //begin
        //Exec(ExpandConstant('{app}\tools\runtimes\vc8\vcredist_x64.exe'), '/Q', '', SW_SHOW,
          //    ewWaitUntilTerminated, ResultCode)
      //end;

      if FirstInstallation then
      begin
        //registrazione menu contestuale a 64 bit
        if FileExists(ExpandConstant('{app}\wscitecm64_en.dll')) then
        begin
          RegisterServer(true, ExpandConstant('{app}\wscitecm64_en.dll'), True);
        end;
        if FileExists(ExpandConstant('{app}\wscitecm64_it.dll')) then
        begin
          RegisterServer(true, ExpandConstant('{app}\wscitecm64_it.dll'), True);
        end;
      end;
    end;

    //avvio installazione runtime VC32bit
    //if FileExists(ExpandConstant('{app}\tools\runtimes\vc8\vcredist_x86.exe')) then
    //begin
      //Exec(ExpandConstant('{app}\tools\runtimes\vc8\vcredist_x86.exe'), '/Q', '', SW_SHOW,
        //      ewWaitUntilTerminated, ResultCode)
    //end;

    //registrazione menu contestuale a 32bit
    if FirstInstallation then
    begin
      if FileExists(ExpandConstant('{app}\wscitecm_en.dll')) then
      begin
        RegisterServer(false, ExpandConstant('{app}\wscitecm_en.dll'), True);
      end;
      if FileExists(ExpandConstant('{app}\wscitecm_it.dll')) then
      begin
        RegisterServer(false, ExpandConstant('{app}\wscitecm_it.dll'), True);
      end;
    end;
  end; //endpostinstall
  
  if (CurStep=ssInstall) then //controllo installazione precedente
  begin //pre installazione
    FirstInstallation := True; //prima installazione
    
    if FileExists(ExpandConstant('{app}\scite.exe')) then
    begin
      if MsgBox(ExpandConstant('{cm:PrevInstall}'), mbConfirmation, MB_YESNO) = IDNO then
      begin
        // user clicked No
        Abort();
      end else
      begin
        // Delete files matching *.properties from old rscite versions
        DelTree(ExpandConstant('{app}\*.properties'), False, True, False);
        // Delete icons from old rscite versions
        DeleteFile(ExpandConstant('{app}\KDiff.lnk'));
        DeleteFile(ExpandConstant('{app}\WinMerge.lnk'));
        DeleteFile(ExpandConstant('{app}\Frhed.lnk'));
        FirstInstallation := False; //upgrade in corso
        //Delete utilities
        //DelTree(ExpandConstant('{app}\*.properties'), False, True, False);
        //DelTree(ExpandConstant('{app}\*.properties'), False, True, False);
        //;Name: {group}\Utilità\Inserisci SciTE in Esplora Risorse; Filename: {app}\regContextMenu.exe; Tasks: ; Languages: italian
        //;Name: {group}\Utilities\Insert SciTE into Explorer; Filename: {app}\regContextMenu.exe; Tasks: ; Languages: english
      end;
    end;

  end; //endpreinstallazione

end; //endCurStepChanged


